/*
  Created by: Fei Song
  File Name: tiny.cup
*/
   
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

    /* Assume true at the start, but set to false with parsing errors */
    public static boolean valid = true;

:};
   

/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal ELSE, IF, WHILE, INT, VOID, RETURN, BOOL;
terminal PLUS, MINUS, TIMES, OVER, LT, LE, GT, GE, EQ, NE, NOT, OR, AND, ASSIGN, SEMI, COMMA, UMINUS;
terminal LPAREN, RPAREN, LBRACKET, RBRACKET, LBRACE, RBRACE;
terminal String ID, NUM, TRUTH;
terminal ERROR;

non terminal DecList program, decList;
non terminal Dec dec, funPrototype;
non terminal VarDec varDeclaration, param;
non terminal FunctionDec funDeclaration;
non terminal NameTy typeSpecifier;
non terminal VarDecList params, paramList, localDeclarations;
non terminal ExpList argList, args, statementList;
non terminal Exp expression, compoundStatement, expressionStatement, iterationStatement, selectionStatement, statement, returnStatement;
non terminal Var var;

precedence nonassoc AND, OR;
precedence nonassoc LT, GT, LE, GE, NE, EQ;
precedence left PLUS, MINUS;
precedence left TIMES, OVER;
precedence right UMINUS, NOT;
precedence left COMMA, ELSE;

/* ----------------------------Grammar Section-------------------- */

start with program;

program ::= decList:dl
            {: RESULT = dl; :}
          ;

decList ::= decList:dl dec:d
            {: DecList t = dl;
               while ( t.tail != null )
                  t = t.tail;
               t.tail = new DecList(d, null);
               RESULT = dl;
            :}
          | dec:d
            {: RESULT = new DecList(d, null); :}
          ;

dec ::= varDeclaration:vd
         {: RESULT = vd; :}
      | funDeclaration:fd
         {:RESULT = fd; :}
      | funPrototype:fp
         {: RESULT = fp; :}
      ;

varDeclaration ::= typeSpecifier:ts ID:id SEMI
                     {: RESULT = new SimpleDec(tsleft, tsright, ts, id); :}
                 | typeSpecifier:ts ID:id LBRACKET NUM:n RBRACKET SEMI
                     {: RESULT = new ArrayDec(tsleft, tsright, ts, id, Integer.parseInt(n)); :}
                 ;

typeSpecifier ::= BOOL:b
                     {:RESULT = new NameTy(bleft, bright, NameTy.BOOL); :}
                | INT:i
                     {:RESULT = new NameTy(ileft, iright, NameTy.INT); :}
                | VOID:v
                     {:RESULT = new NameTy(vleft, vright, NameTy.VOID); :}
                ;

funDeclaration ::= typeSpecifier:ts ID:id LPAREN params:ps RPAREN compoundStatement:cs
                     {: RESULT = new FunctionDec(tsleft, tsright, ts, id, ps, cs); :}
                 ;

funPrototype ::= typeSpecifier:ts ID:id LPAREN params:ps RPAREN SEMI
                     {: RESULT = new FunctionDec(tsleft, tsright, ts, id, ps, null); :}
               ;

params ::= paramList:pl
            {: RESULT = pl; :}
         | VOID
         ;

paramList ::= paramList:pl COMMA param:p
            {: VarDecList t = pl;
               while ( t.tail != null )
                  t = t.tail;
               t.tail = new VarDecList(p, null);
               RESULT = pl;
            :}
            | param:p
               {: RESULT = new VarDecList(p, null); :}
            ;

param ::= typeSpecifier:ts ID:id
            {: RESULT = new SimpleDec(tsleft, tsright, ts, id); :}
        | typeSpecifier:ts ID:id LBRACKET RBRACKET
            {: RESULT = new ArrayDec(tsleft, tsright, ts, id, 0); :}
        ;

compoundStatement ::= LBRACE localDeclarations:ld statementList:sl RBRACE
                        {: RESULT = new CompoundExp(ldleft, ldright, ld, sl); :}
                    ;

localDeclarations ::= localDeclarations:ld varDeclaration:vd
                        {: VarDecList t = ld;
                           while ( t.tail != null )
                              t = t.tail;
                           t.tail = new VarDecList(vd, null);
                           RESULT = ld; 
                        :}
                    ;

statementList ::= statementList:sl statement:s
                     {: ExpList t = sl;
                        while ( t.tail != null )
                           t = t.tail;
                        t.tail = new ExpList(s, null);
                        RESULT = sl;
                     :}
                  |  statement:s
                     {: RESULT = new ExpList(s, null); :}
                ;

statement ::= expressionStatement:es
               {: RESULT = es; :}
            | compoundStatement:cs
               {: RESULT = cs; :}
            | selectionStatement:ss
               {: RESULT = ss; :}
            | iterationStatement:is
               {: RESULT = is; :}
            | returnStatement:rs
               {: RESULT = rs; :}
            ;

expressionStatement ::= expression:e SEMI
                           {: RESULT = e; :}
                      | SEMI
                      ;

selectionStatement ::= IF:i LPAREN expression:e RPAREN statement:s
                        {: RESULT = new IfExp(ileft, iright, e, s, null); :}
                     | IF:i LPAREN expression:e RPAREN statement:s1 ELSE statement:s2
                        {: RESULT = new IfExp(ileft, iright, e, s1, s2); :}
                     ;

iterationStatement ::= WHILE:w LPAREN expression:e RPAREN statement:s
                        {: RESULT = new WhileExp(wleft, wright, e, s); :}
                     ;

returnStatement ::= RETURN:r expression:e SEMI
                     {: RESULT = new ReturnExp(rleft, rright, e); :}
                  | RETURN:r SEMI
                     {: RESULT = new ReturnExp(rleft, rright, null); :}
                  ;

expression ::= var:v ASSIGN expression:e
                {: RESULT = new AssignExp(vleft, vright, new VarExp(vleft, vright, v), e); :}
            | expression:e1 PLUS expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.PLUS, e2); :}
            | expression:e1 MINUS expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.MINUS, e2); :}
            | expression:e1 TIMES expression:e2
                {: RESULT = new OpExp(e1left, e2right, e1, OpExp.TIMES, e2); :}
            | expression:e1 OVER expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.OVER, e2); :}
            | expression:e1 EQ expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.EQ, e2); :}
            | expression:e1 NE expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.NE, e2); :}
            | expression:e1 LT expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.LT, e2); :}
            |  expression:e1 LE expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.LE, e2); :}
            | expression:e1 GT expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.GT, e2); :}
            | expression:e1 GE expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.GE, e2); :}
            | NOT:n expression:e
                {: RESULT = new OpExp(nleft, nright, null, OpExp.NOT, e); :}
            | expression:e1 AND expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.AND, e2); :}
            |  expression:e1 OR expression:e2
                {: RESULT = new OpExp(e1left, e1right, e1, OpExp.OR, e2); :}
            | MINUS:m expression:e
                {: RESULT = new OpExp(mleft, mright, null, OpExp.UMINUS, e); :}
                %prec UMINUS
            | LPAREN expression:e RPAREN
                {: RESULT = e; :}
            | var:v
                {: RESULT = new VarExp(vleft, vright, v); :}
            | ID:id args:a
                {: RESULT = new CallExp(idleft, idright, id, a); :}
            | NUM:n
                {: RESULT = new IntExp(nleft, nright, Integer.parseInt(n)); :}
            | TRUTH:t
                {: RESULT = new BoolExp(tleft, tright, Boolean.parseBoolean(t)); :}
            ;

var ::= ID:id
         {: RESULT = new SimpleVar(idleft, idright, id); :}
      | ID:id LBRACKET expression:e RBRACKET
         {: RESULT = new IndexVar(idleft, idright, id, e); :}
      ;

args ::= argList:al
            {: RESULT = al; :}
       ;

argList ::= argList:al COMMA expression:e
               {: ExpList t = al;
                  while ( t.tail != null )
                     t = t.tail;
                  t.tail = new ExpList(e, null);
                  RESULT = al;
               :}
          | expression:e
               {: RESULT = new ExpList(e, null); :}
          ;

